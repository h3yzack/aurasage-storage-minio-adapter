plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
    id("pl.allegro.tech.build.axion-release") version "1.20.1"
}

group = "io.github.h3yzack"
version = scmVersion.version

repositories {
    mavenLocal()
    maven { url = 'https://jitpack.io' }
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/h3yzack/*")
        credentials {
            username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") as String
            password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key") as String
        }
    }
}

ext{
    aurasageVersion = "1.0.0"
}

dependencies {

    implementation platform("io.github.h3yzack:aurasage-dependencies:${aurasageVersion}")

    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    compileOnly "org.springframework:spring-webflux"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'


    implementation 'software.amazon.awssdk:s3:2.34.5'

    implementation "io.github.h3yzack:aurasage-core"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.5'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named('test') {
    useJUnitPlatform()
}

// Add publishing configuration
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            
            groupId = project.group
            artifactId = 'aurasage-storage-minio-adapter'
            version = project.version
            
            pom {
                name = 'Aurasage Storage MinIO Adapter'
                description = 'MinIO adapter for Aurasage storage service'
                
                developers {
                    developer {
                        id = 'h3yzack'
                        name = 'Zuhaimi A.'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/h3yzack/aurasage-storage-minio-adapter")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") as String
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key") as String
            }
        }
    }
}